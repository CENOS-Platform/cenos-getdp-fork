dnl GetDP - Copyright (C) 1997-2011 P. Dular, C. Geuzaine
dnl
dnl See the LICENSE.txt file for license information. Please report all
dnl bugs and problems to <getdp@geuz.org>.

dnl Process this file with autoconf to produce the configure script.

dnl Check that this is the getdp source tree
AC_INIT(Interface/ProParser.y)

dnl Parse '--with' command-line options
AC_ARG_WITH(blas-lapack-prefix,
            AC_HELP_STRING([--with-blas-lapack-prefix=PFX],
                           [prefix where BLAS and LAPACK are installed]),
            [BLAS_LAPACK_PREFIX=$withval])
AC_ARG_WITH(gmsh-prefix,
            AC_HELP_STRING([--with-gmsh-prefix=PFX],
                           [prefix where Gmsh is installed]),
            [SLEPC_DIR=$withval])
AC_ARG_WITH(gsl-prefix,
            AC_HELP_STRING([--with-gsl-prefix=PFX],
                           [prefix where GSL is installed]),
            [GSL_PREFIX=$withval])
AC_ARG_WITH(petsc-arch,
            AC_HELP_STRING([--with-petsc-arch=ARCH],
                           [PETSc architecture]),
            [PETSC_ARCH=$withval])
AC_ARG_WITH(petsc-prefix,
            AC_HELP_STRING([--with-petsc-prefix=PFX],
                           [prefix where PETSc is installed]),
            [PETSC_DIR=$withval])
AC_ARG_WITH(slepc-prefix,
            AC_HELP_STRING([--with-slepc-prefix=PFX],
                           [prefix where SLEPc is installed]),
            [SLEPC_DIR=$withval])

dnl Parse '--enable' command line options
AC_ARG_ENABLE(arpack,
              AC_HELP_STRING([--enable-arpack],
                             [enable built-in Arpack eigensolver (default=yes)]))
AC_ARG_ENABLE(fortran,
              AC_HELP_STRING([--enable-fortran],
                             [enable Fortran subroutines (default=yes)]))
AC_ARG_ENABLE(gmsh,
              AC_HELP_STRING([--enable-gmsh],
                             [enable remeshing using Gmsh (default=yes)]))
AC_ARG_ENABLE(gsl,
              AC_HELP_STRING([--enable-gsl],
                             [use GSL as numerical toolkit (default=yes)]))
AC_ARG_ENABLE(petsc,
              AC_HELP_STRING([--enable-petsc],
                             [use PETSc linear solvers (default=yes)]))
AC_ARG_ENABLE(slepc,
              AC_HELP_STRING([--enable-slepc],
                             [use SLEPc eigensolvers (default=yes)]))
AC_ARG_ENABLE(sparskit,
              AC_HELP_STRING([--enable-sparskit],
                             [use Sperskit linear solvers (default=no)]))
AC_ARG_ENABLE(zitsol,
              AC_HELP_STRING([--enable-zitsol],
                             [enable support for ZITSOL (default=no)]))

dnl Get the operating system and machine names
UNAME=`uname`
HOSTNAME=`hostname`

dnl Set default option flags
AC_DEFINE(HAVE_LEGACY)
BO="${BO} Legacy"

dnl Check for default compilers
AC_PROG_CC
AC_PROG_CXX
if test "x$enable_fortran" != "xno"; then
  AC_PROG_FC
else
  FC=disabled
  AC_DEFINE(HAVE_NO_FORTRAN)
  BO="${BO} NoFortran"
fi
if test "x${CXX}" = "x" -o "x${FC}" = "x" ; then
  AC_MSG_ERROR([Could not find required compilers, aborting.])
fi

dnl Set preprocessor and linker
AC_PROG_CPP
LINKER="${CXX}"

dnl Set default optimization level
OPTIM="${CXXFLAGS}"

dnl Use c++ for all compilation tests
AC_LANG(C++)

dnl Check for various programs
AC_PROG_CPP
AC_PROG_RANLIB

dnl How to build static libraries?
case "$UNAME" in
  Darwin*)
    RANLIB=true
    AR="libtool -o"
    ;;
  *)
    AC_PROG_RANLIB
    AC_PATH_PROG(AR, ar)
    if test "x${AR}" = "x:"; then
      AC_MSG_ERROR([Could not find the library archiver, aborting.])
    fi
    AR="${AR} ruvs"
    ;;
esac

dnl See if we need a .exe extension on executables
AC_EXEEXT

dnl Check for standard math library
AC_CHECK_LIB(m,main)

dnl Check if Unix98 socklen_t type is available
AC_TRY_COMPILE(
  [#include <sys/types.h>
   #include <sys/socket.h>],
  [socklen_t len = 42; return 0;],,AC_DEFINE(HAVE_NO_SOCKLEN_T) BO="${BO} NoSocklenT")

dnl Set default subdirectories
GETDP_DIRS="Common Numeric Interface Legacy Main"
GETDP_LIBS="-Llib -lGetDPLegacy -lGetDPInterface -lGetDPLegacy -lGetDPNumeric -lGetDPCommon"

dnl Check for PETSc
if test "x$enable_petsc" != "xno" -a "x$enable_sparskit" != "xyes"; then
  if test "x${PETSC_DIR}" = "x"; then
    AC_MSG_ERROR([No PETSC_DIR: run configure again with --with-petsc-prefix.])
  fi
  if test "x${PETSC_ARCH}" = "x"; then
    AC_MSG_WARN([No PETSC_ARCH: you can set it with --with-petsc-arch.])
  fi
  AC_CHECK_FILE(${PETSC_DIR}/bmake/${PETSC_ARCH}/petscconf.h,PETSC="yes",PETSC="no")
  if test "x${PETSC}" = "xyes"; then
    PETSC_INCLUDE_MAKEFILE='include ${PETSC_DIR}/bmake/common/variables'
  else
    AC_CHECK_FILE(${PETSC_DIR}/${PETSC_ARCH}/include/petscconf.h,PETSC="yes",PETSC="no")
    if test "x${PETSC}" = "xyes"; then
      PETSC_INCLUDE_MAKEFILE='include ${PETSC_DIR}/conf/variables'
    else
      AC_MSG_ERROR([Could not find PETSC. Use --enable-sparskit instead?])
    fi
  fi
  LAPACK="yes"
  LINKER="\${CLINKER}"
  FLAGS="${FLAGS} \${PETSCFLAGS} \${PETSC_INCLUDE} \${PETSC_CC_INCLUDES}"
  AC_DEFINE(HAVE_PETSC)
  BO="${BO} Petsc"
fi

dnl Check for SLEPc
if test "x${PETSC}" = "xyes" -a "x$enable_slepc" != "xno"; then
  if test "x${SLEPC_DIR}" = "x"; then
    AC_MSG_WARN([No SLEPC_DIR: could not find SLEPC.])
  else
    AC_CHECK_FILE(${SLEPC_DIR}/include/slepcqep.h,SLEPC="yes",SLEPC="no")
    if test "x${SLEPC}" = "xyes"; then
      GETDP_LIBS="${GETDP_LIBS} -L${SLEPC_DIR}/${PETSC_ARCH}/lib -lslepc"
      FLAGS="${FLAGS} -I${SLEPC_DIR}/include -I${SLEPC_DIR}/${PETSC_ARCH}/include"
      AC_DEFINE(HAVE_SLEPC)
      BO="${BO} Slepc"
    else
      AC_MSG_WARN([Did not find QEP (available in SLEPC >= 3.1): disabling SLEPC.])
    fi
  fi
fi

dnl Check for Sparskit
if test "x$enable_sparskit" = "xyes"; then
  AC_CHECK_FILE(./contrib/Sparskit/Sparskit.cpp,SPARSKIT="yes",SPARSKIT="no")
  if test "x${SPARSKIT}" = "xno"; then
    AC_MSG_ERROR([Could not find Sparskit.])
  fi
  GETDP_DIRS="${GETDP_DIRS} contrib/Sparskit"
  GETDP_LIBS="${GETDP_LIBS} -lGetDPSparskit"
  AC_DEFINE(HAVE_SPARSKIT)
  BO="${BO} Sparskit"
fi

dnl Check for blas and lapack by hand if we don't have PETSc
if test "x${PETSC}" != "xyes"; then
  if test "x$enable_fortran" != "xno"; then
    AC_LANG_PUSH(Fortran 77)
  fi
  if test "x${BLAS_LAPACK_PREFIX}" != "x"; then
    LDFLAGS="${LDFLAGS} -L${BLAS_LAPACK_PREFIX} -L${BLAS_LAPACK_PREFIX}/lib"
  fi
  AC_CHECK_LIB(blas,dasum,BLAS="yes",BLAS="no")
  if test "x${BLAS}" = "xyes"; then
    AC_CHECK_LIB(lapack,dbdsqr,LAPACK="yes",LAPACK="no",-lblas)
  fi
  if test "x$enable_fortran" != "xno"; then
    AC_LANG_POP()
  fi
fi

dnl Check for Arpack, then link with blas and lapack if necessary
if test "x$enable_fortran" != "xno"; then
  if test "x$enable_arpack" != "xno" -a "x${LAPACK}" = "xyes"; then
    AC_CHECK_FILE(./contrib/Arpack/znaupd.f, ARPACK="yes", ARPACK="no")
    if test "x${ARPACK}" = "xyes"; then
      AC_DEFINE(HAVE_ARPACK)
      BO="${BO} Arpack"
      GETDP_DIRS="${GETDP_DIRS} contrib/Arpack"
      GETDP_LIBS="${GETDP_LIBS} -lGetDPArpack"
      if test "x${SPARSKIT}" = "xyes"; then
        if test "x${BLAS_LAPACK_PREFIX}" != "x"; then
          GETDP_LIBS="${GETDP_LIBS} -L${BLAS_LAPACK_PREFIX} -llapack -lblas"
        else
          GETDP_LIBS="${GETDP_LIBS} -llapack -lblas"
        fi
      fi
    fi
  fi
fi

dnl check for zitsol
if test "x$enable_fortran" != "xno"; then
  if test "x$enable_zitsol" = "xyes" -a "x${PETSC}" = "xyes"; then
    AC_CHECK_FILE(./contrib/ZITSOL_1/zarms2.c,ZITSOL="yes",ZITSOL="no")
    if test "x${ZITSOL}" = "xyes"; then
      AC_DEFINE(HAVE_ZITSOL)
      BO="${BO} Zitsol"
      GETDP_DIRS="${GETDP_DIRS} contrib/ZITSOL_1"
      GETDP_LIBS="${GETDP_LIBS} -lGetDPzitsol"
    fi
  fi
fi

dnl Check for Gmsh
if test "x$enable_gmsh" != "xno"; then
  if test "x${GMSH_PREFIX}" != "x"; then
    LDFLAGS="-L${GMSH_PREFIX} -L${GMSH_PREFIX}/lib ${LDFLAGS}"
  fi
  AC_CHECK_LIB(Gmsh,main,GMSH="yes",GMSH="no")
  if test "x${GMSH}" = "xyes"; then
    AC_DEFINE(HAVE_GMSH)
    BO="${BO} Gmsh"
    if test "x${GMSH_PREFIX}" = "x"; then
      GETDP_LIBS="${GETDP_LIBS} -lGmsh"
    else
      GETDP_LIBS="${GETDP_LIBS} -L${GMSH_PREFIX} -L${GMSH_PREFIX}/lib -lGmsh"
      FLAGS="${FLAGS} -I${GMSH_PREFIX} -I${GMSH_PREFIX}/include"
      FLAGS="${FLAGS} -I${GMSH_PREFIX}/gmsh -I${GMSH_PREFIX}/include/gmsh"
    fi
  fi
fi

dnl Check for GSL 
if test "x$enable_gsl" != "xno"; then
  if test "x${GSL_PREFIX}" != "x"; then
    LDFLAGS="-L${GSL_PREFIX} -L${GSL_PREFIX}/lib ${LDFLAGS}"
  fi
  AC_CHECK_LIB(gslcblas,main)
  AC_CHECK_LIB(gsl,main,GSL="yes",GSL="no")
  if test "x${GSL}" = "xyes"; then
    AC_DEFINE(HAVE_GSL)
    BO="${BO} Gsl"
    if test "x${GSL_PREFIX}" = "x"; then
      GETDP_LIBS="${GETDP_LIBS} -lgsl -lgslcblas"
    else
      GETDP_LIBS="${GETDP_LIBS} -L${GSL_PREFIX} -L${GSL_PREFIX}/lib -lgsl -lgslcblas"
      FLAGS="${FLAGS} -I${GSL_PREFIX} -I${GSL_PREFIX}/include"
    fi
  fi
fi
if test "x${GSL}" != "xyes"; then
  dnl Check if non-free numerical recipes routines are in the tree
  AC_CHECK_FILE(./contrib/NR/dsvdcmp.cpp,NR="yes",NR="no")
  if test "x${NR}" = "xyes"; then
    AC_MSG_WARN([You are building a non-free version of GetDP, using code copyright])
    AC_MSG_WARN([(C) 1986-92 Numerical Recipes Software J!0.])
    AC_MSG_WARN([To use the GSL instead, run configure again with the --enable-gsl])
    AC_MSG_WARN([option.])
    GETDP_DIRS="${GETDP_DIRS} contrib/NR"
    GETDP_LIBS="${GETDP_LIBS} -lGetDPNR"
  else
    AC_MSG_ERROR([Could not find GSL or NR, aborting.])
  fi
fi

dnl Add PETSc libraries at the end (they include blas and lapack)
if test "x${PETSC}" = "xyes"; then
  GETDP_LIBS="${GETDP_LIBS} \${PETSC_SNES_LIB} \${PETSC_KSP_LIB}"
else
  dnl Add necessary Fortran libraries
  case "${FC}" in
    *gfortran*)
      GETDP_LIBS="${GETDP_LIBS} -lgfortran"
      ;;
    *g77*)
      GETDP_LIBS="${GETDP_LIBS} -lg2c"
      ;;
  esac
fi

dnl Force specific options for native compilers
case "${UNAME}" in
  CYGWIN* | MINGW*)
    if test "x$enable_cygwin" != "xyes"; then
      GETDP_LIBS="${GETDP_LIBS} -lwsock32"
    fi
    dnl increase stack size to 16Mb to avoid stack overflows in
    dnl recursive calls (cf. Cal_Quantity) + try to use static
    dnl versions of gcc libs to ease distribution
    LINKER="${LINKER} -Wl,--stack,16777216 -static"
    ;;
esac

dnl Finish link line
GETDP_LIBS="${GETDP_LIBS} -lm"    

AC_DEFINE_UNQUOTED(GETDP_CONFIG_OPTIONS, "${BO}")
AC_CONFIG_HEADER(Common/GetDPConfig.h:Common/GetDPConfig.h.in)

dnl Write output
AC_SUBST(UNAME)
AC_SUBST(HOSTNAME)
AC_SUBST(FLAGS)
AC_SUBST(OPTIM)
AC_SUBST(LINKER)
AC_SUBST(GETDP_DIRS)
AC_SUBST(GETDP_LIBS)
AC_SUBST(AR)
AC_SUBST(PETSC_INCLUDE_MAKEFILE)
AC_SUBST(PETSC_DIR)
AC_SUBST(PETSC_ARCH)
AC_OUTPUT(variables)

dnl Print some information
echo ""
echo "GetDP has been configured for ${UNAME} with the following options:${BO}"
echo ""
echo "C compiler: ${CC}"
echo "C++ compiler: ${CXX}"
echo "Fortran compiler: ${FC}"
echo "Linker: ${LINKER}"
echo "Optimization flags: ${OPTIM}"
if test "x${PETSC}" = "xyes"; then
  echo "PETSC_DIR: ${PETSC_DIR}"
  echo "PETSC_ARCH: ${PETSC_ARCH}"
fi
echo ""
echo "Edit 'variables' and 'Common/GetDPConfig.h' to fine-tune the configuration."
