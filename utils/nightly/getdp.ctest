# GetDP - Copyright (C) 1997-2017 P. Dular and C. Geuzaine, University of Liege
#
# See the LICENSE.txt file for license information. Please report all
# bugs and problems to the public mailing list <getdp@onelab.info>.

# this is the common ctest script used by all getdp nightly (and experimental)
# builds, on all architectures

set(CTEST_SITE "ace36.montefiore")

find_program(CTEST_GIT_COMMAND NAMES git)
find_program(CTEST_COVERAGE_COMMAND NAMES gcov)
find_program(CTEST_MEMORYCHECK_COMMAND NAMES valgrind)

if(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
  set(CTEST_BUILD_CONFIGURATION "Debug")
else(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
  set(CTEST_BUILD_CONFIGURATION "RelWithDebInfo")
endif(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)

set(CTEST_SOURCE_DIRECTORY "$ENV{HOME}/src/getdp")
set(CTEST_BINARY_DIRECTORY "$ENV{HOME}/src/getdp/bin")
set(CTEST_GIT_REPOS https://gitlab.onelab.info/getdp/getdp.git)
set(CTEST_CMAKE_GENERATOR "Unix Makefiles")

if(NOT EXISTS "${CTEST_SOURCE_DIRECTORY}")
  set(CTEST_CHECKOUT_COMMAND 
      "${CTEST_GIT_COMMAND} pull ${CTEST_GIT_REPOS} ${CTEST_SOURCE_DIRECTORY}")
endif(NOT EXISTS "${CTEST_SOURCE_DIRECTORY}")
set(CTEST_UPDATE_COMMAND "${CTEST_GIT_COMMAND}")

set(CTEST_CONFIGURE_COMMAND 
    "${CTEST_CMAKE_COMMAND} -DCMAKE_BUILD_TYPE:STRING=${CTEST_BUILD_CONFIGURATION}")
set(CTEST_CONFIGURE_COMMAND
    "${CTEST_CONFIGURE_COMMAND} \"-G${CTEST_CMAKE_GENERATOR}\"")
set(CTEST_CONFIGURE_COMMAND
    "${CTEST_CONFIGURE_COMMAND} ${CTEST_BUILD_OPTIONS}")
if(NOT APPLE) # FIXME - testing!
  set(CTEST_CONFIGURE_COMMAND
      "${CTEST_CONFIGURE_COMMAND} -DENABLE_PYTHON=0")
endif(NOT APPLE)
if(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
  set(CTEST_CONFIGURE_COMMAND 
      "${CTEST_CONFIGURE_COMMAND} \"-DCMAKE_CXX_FLAGS=-g -O0 -fprofile-arcs -ftest-coverage\"")
  set(CTEST_CONFIGURE_COMMAND
      "${CTEST_CONFIGURE_COMMAND} \"-DCMAKE_C_FLAGS=-g -O0 -fprofile-arcs -ftest-coverage\"")
  set(CTEST_CONFIGURE_COMMAND
      "${CTEST_CONFIGURE_COMMAND} \"-DCMAKE_EXE_LINKER_FLAGS=-fprofile-arcs -ftest-coverage\"")
endif(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} \"${CTEST_SOURCE_DIRECTORY}\"")

# make sure everybody outputs messages in english
set($ENV{LC_MESSAGES} "en_US")

# select Experimental dashboard if none is selected
if(NOT CDASH_MODEL)
  set(CDASH_MODEL "Experimental")
endif(NOT CDASH_MODEL)

ctest_empty_binary_directory(${CTEST_BINARY_DIRECTORY})
ctest_start(${CDASH_MODEL})
# update from Git only for nightly builds
if(CDASH_MODEL MATCHES "Nightly")
  ctest_update()
endif(CDASH_MODEL MATCHES "Nightly")
ctest_configure()
ctest_build(TARGET html)
ctest_build(TARGET package NUMBER_ERRORS ERR)

set(ENV{GETDP_BUILD_ERRORS} ${ERR})

if(WITH_TEST)
  ctest_test()
endif(WITH_TEST)
if(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
  ctest_coverage()
endif(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
if(WITH_MEMCHECK AND CTEST_MEMORYCHECK_COMMAND)
  ctest_memcheck()
endif(WITH_MEMCHECK AND CTEST_MEMORYCHECK_COMMAND)
ctest_submit()

macro(upload_package WEB ARCH EXTRA EXT1 EXT2)
  if($ENV{GETDP_BUILD_ERRORS} GREATER 0)
    message("Build reported $ENV{GETDP_BUILD_ERRORS} errors: skipping upload")
    set(ENV{GETDP_BUILD_ERRORS} 0)
  else($ENV{GETDP_BUILD_ERRORS} GREATER 0)
    message("Build succeeded: uploading package to web site")
    file(GLOB FILE ${CTEST_BINARY_DIRECTORY}/getdp-*${EXT1})
    if(FILE)
      get_filename_component(BASENAME ${FILE} NAME)
      if(APPLE) # re-sign package on ace36 (MacOSX 10.7 is too old to produce v2 sigs)
        message("Re-signing MacOS build on ace36")
        execute_process(COMMAND scp -C ${FILE} ace@ace36.montefiore.ulg.ac.be:/tmp
                        TIMEOUT 60)
        execute_process(COMMAND ssh ace@ace36.montefiore.ulg.ac.be /Users/ace/re-sign.sh /tmp/${BASENAME}
                        TIMEOUT 60)
        execute_process(COMMAND scp -C ace@ace36.montefiore.ulg.ac.be:/tmp/${BASENAME} ${FILE}
                        TIMEOUT 60)
        message("Done re-signing MacOS build on ace36")
      endif(APPLE)
      message("Build succeeded: uploading package ${BASENAME} to web site")
      if(${FILE} MATCHES "-git")
        execute_process(COMMAND scp -C ${FILE} ${WEB}/getdp-git-${ARCH}${EXTRA}${EXT2}
                        TIMEOUT 60)
      else(${FILE} MATCHES "-git")
        get_filename_component(NAME ${FILE} NAME)
        string(REPLACE ${EXT1} "" BASE ${NAME})
        execute_process(COMMAND scp -C ${FILE} ${WEB}/${BASE}${EXTRA}${EXT2}
                        TIMEOUT 60)
      endif(${FILE} MATCHES "-git")
      message("Done uploading package to web site")
    endif(FILE)
  endif($ENV{GETDP_BUILD_ERRORS} GREATER 0)
endmacro(upload_package)
